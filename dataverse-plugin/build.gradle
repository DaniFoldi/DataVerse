plugins {
    id 'java'
    id 'net.kyori.blossom' version '1.3.1'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group parent.group
version parent.version

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://repo.danifoldi.com/repository/maven-releases/' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
}

dependencies {
    // api
    implementation project(':')

    // platforms
    compileOnly 'io.github.waterfallmc:waterfall-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'

    // internal libraries
    implementation 'com.danifoldi:DML:1.0.2'
    implementation 'com.danifoldi:microbase:0.7.1'

    // db connectors
    implementation ('com.zaxxer:HikariCP:5.0.1') {
        exclude module: 'slf4j-api'
    }
    implementation ('org.mariadb.jdbc:mariadb-java-client:3.2.0') {
        exclude module: 'checker-qual'
        exclude module: 'jcl-over-slf4j'
        exclude module: 'slf4j-api'
    }
    implementation 'org.mongodb:mongo-java-driver:3.12.14'
    implementation 'mysql:mysql-connector-java:8.0.33'

    // intellij
    compileOnly 'org.jetbrains:annotations:23.1.0'
}

processResources {
    from('src/main/resources') {
        expand(version: project.version)
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }
}

blossom {
    replaceTokenIn('src/main/java/com/danifoldi/dataverse/platform/velocity/DataVersePlugin.java')
    replaceToken '@version@', project.version
}

shadowJar {
    minimize()

    relocate 'com.zaxxer.hikari', 'com.danifoldi.dataverse.lib.hikari'
    relocate 'com.danifoldi.dml', 'com.danifoldi.dataverse.lib.dml'
    relocate 'com.danifoldi.microbase', 'com.danifoldi.dataverse.lib.microbase'
    relocate 'org.mariadb', 'com.danifoldi.dataverse.lib.mariadb'
    relocate 'com.mongodb', 'com.danifoldi.dataverse.lib.mongodb'
    relocate 'com.google.common', 'com.danifoldi.dataverse.lib.common'
    relocate 'com.google.errorprone', 'com.danifoldi.dataverse.lib.errorprone'
    relocate 'com.google.protobuf', 'com.danifoldi.dataverse.lib.protobuf'
    relocate 'google.protobuf', 'com.danifoldi.dataverse.lib.protobuf2'
    relocate 'com.mysql', 'com.danifoldi.dataverse.lib.mysql'
    relocate 'org.bson', 'com.danifoldi.dataverse.lib.bson'
    relocate 'javax.inject', 'com.danifoldi.dataverse.lib.inject'
    relocate 'dagger', 'com.danifoldi.dataverse.lib.dagger'

    getArchiveFileName().set("${project.name}-${project.version}.jar")
}